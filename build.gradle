plugins {
    id "java"
    id "jacoco"
    id "org.springframework.boot" version "2.4.5"
    id 'info.solidsoft.pitest' version '1.6.0'
    id "org.sonarqube" version "3.2.0"
    id 'maven-publish'
}

apply plugin: "io.spring.dependency-management"

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

group = "com.github.mikesafonov"
version = "1.1.1"

repositories {
    mavenCentral()
}

wrapper {
    gradleVersion = "6.3"
}

bootJar {
    archiveFileName = "jenkins-telegram-chatops.jar"
    launchScript()
}

springBoot {
    buildInfo {
        properties {
            additional = [
                    version    : project.version
            ]
        }
    }
}


dependencyManagement {
    imports {
        mavenBom("org.springframework.cloud:spring-cloud-dependencies:2020.0.3")
    }
}


test{
    useJUnitPlatform()
    finalizedBy(jacocoTestReport)
}

configurations {
    implementation.exclude module: "spring-boot-starter-logging"
}


dependencies {
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-starter-log4j2")
    implementation("org.springframework.boot:spring-boot-starter-actuator")
    implementation("org.springframework.boot:spring-boot-starter-validation")
    runtimeOnly("org.springframework.boot:spring-boot-starter-aop")
    implementation("org.springframework.retry:spring-retry")
    implementation('org.springframework.cloud:spring-cloud-starter-bootstrap')
    implementation("org.springframework.cloud:spring-cloud-starter-vault-config")

    implementation("org.telegram:telegrambots-spring-boot-starter:5.4.0")

    implementation("com.vdurmont:emoji-java:5.1.1")

    implementation("io.micrometer:micrometer-core")
    implementation("io.micrometer:micrometer-registry-prometheus")

    implementation("com.offbytwo.jenkins:jenkins-client:0.3.8")

    implementation("org.apache.logging.log4j:log4j-web")

    annotationProcessor("org.springframework.boot:spring-boot-configuration-processor")

    compileOnly("org.projectlombok:lombok")
    annotationProcessor("org.projectlombok:lombok")

    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: "junit", module: "junit"
        exclude module: "junit-vintage-engine"
    }
    testCompileOnly("org.projectlombok:lombok")
    testAnnotationProcessor("org.projectlombok:lombok")

    testImplementation("org.assertj:assertj-core")
}

jacoco {
    toolVersion = "0.8.3"
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled = false
    }
}

pitest {
    junit5PluginVersion = '0.14'
    targetClasses = ["com.github.mikesafonov.jenkins.telegram.chatops.bot.*",
                     "com.github.mikesafonov.jenkins.telegram.chatops.jenkins.*",
                     "com.github.mikesafonov.jenkins.telegram.chatops.utils.*"]
    excludedClasses = ["com.github.mikesafonov.jenkins.telegram.chatops.jenkins.model.*"]
    threads = 2
    outputFormats = ["XML", "HTML"]
    avoidCallsTo = ["org.apache.logging.log4j.Logger"]
    timestampedReports = false
    enableDefaultIncrementalAnalysis = true
    historyInputLocation = ".pitest/pitHistory.txt"
    historyOutputLocation = ".pitest/pitHistory.txt"
}

sonarqube {
    properties {
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.organization', 'mikesafonov-github'
        property 'sonar.projectKey', 'MikeSafonov_jenkins-telegram-chatops'
        property 'sonar.projectName', 'jenkins-telegram-chatops'
        property 'sonar.projectVersion', '1.1.1'
        property 'sonar.sources', 'src/main'
        property 'sonar.tests', 'src/test'
        property 'sonar.coverage.jacoco.xmlReportPaths', 'build/reports/jacoco/test/jacocoTestReport.xml'
        property 'sonar.coverage.exclusions', '**/main/resources/**, **/com/github/mikesafonov/jenkins/telegram/chatops/config/*, **/com/github/mikesafonov/jenkins/telegram/chatops/dto/*,' +
                '  **/com/github/mikesafonov/jenkins/telegram/chatops/jenkins/model/*'
        property 'sonar.exclusions', '**/main/resources/**, **/com/github/mikesafonov/jenkins/telegram/chatops/config/*, **/com/github/mikesafonov/jenkins/telegram/chatops/dto/*,' +
                '  **/com/github/mikesafonov/jenkins/telegram/chatops/jenkins/model/*'
        property 'sonar.java.binaries', 'build/classes'
        property 'sonar.links.homepage', 'https://github.com/MikeSafonov/jenkins-telegram-chatops'
        property 'sonar.links.ci', 'https://github.com/MikeSafonov/jenkins-telegram-chatops'
        property 'sonar.links.scm', 'https://github.com/MikeSafonov/jenkins-telegram-chatops'
        property 'sonar.links.issue', 'https://github.com/MikeSafonov/jenkins-telegram-chatops/issues'
    }
}


publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/MikeSafonov/jenkins-telegram-chatops"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
