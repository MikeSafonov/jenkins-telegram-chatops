plugins {
    id "java"
    id "jacoco"
    id "org.springframework.boot" version "2.2.7.RELEASE"
    id 'info.solidsoft.pitest' version '1.5.1'
}

apply plugin: "io.spring.dependency-management"

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

group = "com.github.mikesafonov"
version = "0.1.0"

repositories {
    mavenCentral()
    jcenter()
}

wrapper {
    gradleVersion = "6.3"
}

bootJar {
    archiveFileName = "jenkins-telegram-chatops.jar"
    launchScript()
}

springBoot {
    buildInfo {
        properties {
            additional = [
                    version    : project.version
            ]
        }
    }
}


dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:Hoxton.SR4"
    }
}


test{
    useJUnitPlatform()
    finalizedBy(jacocoTestReport)
}

configurations {
    implementation.exclude module: "spring-boot-starter-logging"
}


dependencies {
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-starter-log4j2")
    implementation("org.springframework.boot:spring-boot-starter-actuator")
    runtime("org.springframework.boot:spring-boot-starter-aop")
    implementation("org.springframework.retry:spring-retry")
    implementation("org.springframework.cloud:spring-cloud-starter-vault-config")

    implementation("org.telegram:telegrambots-spring-boot-starter:4.8.1")

    implementation("io.micrometer:micrometer-core")
    implementation("io.micrometer:micrometer-registry-prometheus")

    implementation("com.offbytwo.jenkins:jenkins-client:0.3.8")

    implementation("org.apache.logging.log4j:log4j-web")

    implementation("io.micrometer:micrometer-core")
    implementation("io.micrometer:micrometer-registry-prometheus")

    annotationProcessor("org.springframework.boot:spring-boot-configuration-processor")

    compileOnly("org.projectlombok:lombok")
    annotationProcessor("org.projectlombok:lombok")

    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: "junit", module: "junit"
        exclude module: "junit-vintage-engine"
    }
    testCompileOnly("org.projectlombok:lombok")
    testAnnotationProcessor("org.projectlombok:lombok")

    testImplementation("org.assertj:assertj-core")
}

jacoco {
    toolVersion = "0.8.3"
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled = false
    }
}

pitest {
    junit5PluginVersion = '0.12'
    targetClasses = ["com.github.mikesafonov.jenkins.telegram.chatops.bot.*",
                     "com.github.mikesafonov.jenkins.telegram.chatops.jenkins.*"]
    excludedClasses = ["com.github.mikesafonov.jenkins.telegram.chatops.jenkins.model.*"]
    threads = 2
    outputFormats = ["XML", "HTML"]
    avoidCallsTo = ["org.apache.logging.log4j.Logger"]
    timestampedReports = false
    enableDefaultIncrementalAnalysis = true
    historyInputLocation = ".pitest/pitHistory.txt"
    historyOutputLocation = ".pitest/pitHistory.txt"
}

